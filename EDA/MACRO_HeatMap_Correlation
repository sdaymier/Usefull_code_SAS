/* ******************************************************************** */
/* >>> Macro permettant de construire la matrice de corrélation 		*/
/* ******************************************************************** */
%macro prepCorrData(in=,out=);

	data tmp;
		set &in.;
		keep _numeric_;
		drop &var_cible. hors_scope;
	run;
	/* Run corr matrix for input data, all numeric vars */
	proc corr data=tmp noprint
		pearson
		outp=_tmpCorr
		vardef=df
	;run;

	/* prep data for heat map */
	data &out.;
		keep x y r;
		set _tmpCorr(where=(_TYPE_="CORR"));
		array v{*} _numeric_;
		x = _NAME_;
		do i = dim(v) to 1 by -1;
		y = vname(v(i));
		r = v(i);
		/* creates a lower triangular matrix */
		if (i<_n_) then
		  r=.;
		output;
		end;
	run;

	proc delete data = tmp _tmpcorr; run;

%mend;


/* ******************************************************************** */
/* >>> Macro permettant de créer la heatmap								*/
/* ******************************************************************** */

%macro heatmap_correlation(tabin = , tabout = );

	/* ---------------------------------------------------------------- */
	/* 1. Création de la matrice de corrélation 						*/
	/* ---------------------------------------------------------------- */
	%prepCorrData(in=&tabin.,out=tab_corr);

	/* ---------------------------------------------------------------- */
	/* 2. Construction du template a remplir avec les données 			*/
	/* ---------------------------------------------------------------- */
	ods path work.mystore(update) sashelp.tmplmst(read);
	proc template;
	  define statgraph corrHeatmap;
	   dynamic _Title;
	    begingraph;
	      entrytitle _Title;
	      rangeattrmap name='map';
	      /* Get ideas from http://colorbrewer.org                   */
	      range -1 - 1 / rangecolormodel=(cxD8B365 cxF5F5F5 cx5AB4AC);
	      endrangeattrmap;
	      rangeattrvar var=r attrvar=r attrmap='map';
	      layout overlay / 
	        xaxisopts=(display=(line ticks tickvalues)) 
	        yaxisopts=(display=(line ticks tickvalues));
	        heatmapparm x = x y = y colorresponse = r / 
	          xbinaxis=false ybinaxis=false
	          name = "heatmap" display=all;
	        continuouslegend "heatmap" / 
	          orient = vertical location = outside title="Pearson Correlation";
	      endlayout;
	    endgraph;
	  end;
	run;

	/* ---------------------------------------------------------------- */
	/* 3. Affichage de la heatmap 										*/
	/* ---------------------------------------------------------------- */
	proc sgrender data=&tabout. template=corrHeatmap;
	   dynamic _title="Corr matrix";
	run;

%mend;



